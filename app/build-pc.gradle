plugins {
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id "com.github.johnrengelman.shadow" version "4.0.2"
    id "org.jetbrains.kotlin.jvm" version "1.2.61"
    id "org.jetbrains.kotlin.kapt" version "1.2.61"
    id "org.jetbrains.kotlin.plugin.allopen" version "1.2.61"
    id 'org.hidetake.swagger.generator' version '2.15.0'
    id "com.gorylenko.gradle-git-properties" version "1.5.2"
}

apply plugin: "application"
apply plugin: "groovy"

version "0.1"
group "jl.pc"

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://jcenter.bintray.com" }
    jcenter()
}

dependencyManagement {
    imports {
        mavenBom 'io.micronaut:micronaut-bom:1.0.1'
    }
}

dependencies {
    compile "io.micronaut:micronaut-http-client"
    compile "io.micronaut.configuration:micronaut-micrometer-core"
    compile "io.micronaut.configuration:micronaut-micrometer-registry-prometheus"
    compile "io.swagger.core.v3:swagger-annotations"
    // compile "io.micronaut.configuration:micronaut-netflix-hystrix"
    compile "io.micronaut:micronaut-discovery-client"
    compile "io.micronaut:micronaut-http-server-netty"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"
    compile "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
    compile "io.micronaut:micronaut-runtime"
    compile "io.micronaut:micronaut-management"

    compile project(":api")
    compile "io.github.microutils:kotlin-logging:1.5.9"
    
    kapt "io.micronaut.configuration:micronaut-openapi"
    kapt "io.micronaut:micronaut-inject-java"
    kapt "io.micronaut:micronaut-validation"
    kaptTest "io.micronaut:micronaut-inject-java"
    runtime "ch.qos.logback:logback-classic:1.2.3"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.4.1"
    testCompile("org.spockframework:spock-core") {
        exclude group: "org.codehaus.groovy", module: "groovy-all"
    }
    testCompile "io.micronaut:micronaut-inject-groovy"
    testCompile "org.junit.jupiter:junit-jupiter-api:5.1.0"
    testCompile "org.jetbrains.spek:spek-api:1.1.5"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.1.0"
    testRuntime "org.jetbrains.spek:spek-junit-platform-engine:1.1.5"

    swaggerUI 'org.webjars:swagger-ui:3.10.0'

}

swaggerSources {
    app {
        inputFile = file("$buildDir/tmp/kapt3/classes/main/META-INF/swagger/product-catalogue-service-api-0.0.yml")
        ui {
            outputDir = file("$buildDir/tmp/kapt3/classes/main/META-INF/swagger-ui")
        }
        reDoc {
            outputDir = file("$buildDir/tmp/kapt3/classes/main/META-INF/redoc")
            scriptSrc = "https://cdn.jsdelivr.net/npm/redoc@next/bundles/redoc.standalone.js"
        }
    }
}

shadowJar {
    mergeServiceFiles()
}

run.jvmArgs('-noverify', '-XX:TieredStopAtLevel=1')

mainClassName = "jl.pc.v0.ProductCatalogueApplication"

test {
    useJUnitPlatform()
}

allOpen {
	annotation("io.micronaut.aop.Around")
}

compileKotlin {
	kotlinOptions {
	    jvmTarget = '1.8' 
	    //Will retain parameter names for Java reflection
	    javaParameters = true 
	}
}

compileTestKotlin {
	kotlinOptions {
	    jvmTarget = '1.8' 
	    javaParameters = true 
	}
}
